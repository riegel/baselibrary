#
  Housekeeping... We need to clear some vars, we clear them here and should also clear
  them again right before CLEAR_PUSH_DIVSTORE finishes.
/#
GLOBAL_GOTO_PAGE='ERROR'
CLEAR_DIVSTORE='ERROR'
ajaxrequest='ERROR'
ajax.CLICKED='ERROR'

function DLLendAJAX() do return ajaxGOTO('none') /return /function
function DLLendOVERLAY() do return ajaxGOTO('') /return /function
function DLLajaxGOTO(p) locals text,f do
 if p='ERROR' or p='' then p=page /if

 if p=page and right(page,8)='.overlay' then
  GLOBAL_GOTO_PAGE=getenv('HTTP_REFERER')
  if GLOBAL_GOTO_PAGE='' then
   GLOBAL_GOTO_PAGE=getlink3('/apps/clear/externaloverlayerror.html','ERROR_PAGE',page)
  /if
 else
  GLOBAL_GOTO_PAGE=p
 /if

 if DLLajaxrequest()='TRUE' then
  if p=page or count(p,'.')=0 or count(p,'.overlay')=1 then
   if p=page then
    text=^o=CLEAR_PUSH_DIVSTORE()^
    f=^/apps/clear/goto_ajax.lib^
   else
    # looks like we are performing a goto to another overlay, we will let that overlay do the ajax thing /#
    p=getlink3(p,'ajaxrequest','TRUE')
    text=^goto "^+p+^"^
    f=^/apps/clear/goto_page.lib^
   /if
  else
   # Do a goto inside the ajax environment /#
   if left(p,7)='http://' or left(p,8)='https://' then
    o=runSCRIPT(^window.location='^+p+^';^)
   else
    o=runSCRIPT(^window.location='^+getlink1(p)+^';^)
   /if
   text=^o=CLEAR_PUSH_DIVSTORE()^
   f=^/apps/clear/goto_ajax.lib^
  /if
 else
  # no ajax so... we just do the goto, we also clear any AJAX related variables first /#
  CLEAR_DIVSTORE='ERROR'
  ajaxrequest='ERROR'
  ajax.CLICKED='ERROR'
  if GLOBAL_GOTO_PAGE<>'none' then
   text=^goto "^+GLOBAL_GOTO_PAGE+^"^
   f=^/apps/clear/goto_page.lib^
  else
   text=''
   f=^/apps/clear/goto_null.lib^
  /if
 /if
 if version<4.5 then
  return f /return
 else
  expand text /expand
 /if
 goto "/apps/clear/version4.54.html"
/function




function DLLupdateVARS(list) locals x,h,v do
 if rows(list)=1 and (count(list,' ')>0 or count(list,' ')>0) then
  if count(list,',')>0 then
   list=split(list,',')
  else
   list=split(list,' ')
  /if
 /if
 for name=list rowname=x do
  x[1]=trim(x[1])
  z=indirection(x[1])
  if cols(z)>1 then
   for name=z rownum=v do
    for name=z colnum=h do
     updatesinglevar(x[1]+'['+h+','+v+']')
    /for
   /for
  else
   updatesinglevar(x[1])
  /if
 /for
/function

function updatesinglevar(var) locals x,z,y,o do
 var=trim(var)
 z=indirection(var)
 y=CLEAR_arrayJS(z)
 o=writeHTML('evalJS',^CLEAR.f.setvalue('^+var+^',^+y+^);  //Excellent^)
/function




















function DLLwriteHTML(id,html) locals temp do
 if id<>'evalJS' then
  temp='CLEAR_'+id
  @temp=html
 /if
 temp[1]=id temp[2]=html
 CLEAR_DIVSTORE=appends(CLEAR_DIVSTORE,temp)
 return '' /return
/function





function DLLappendHTML(id,html) locals temp do
 temp[1]=id temp[2]=html temp[3]='APPEND'
 CLEAR_DIVSTORE=appends(CLEAR_DIVSTORE,temp)
 return '' /return
/function


function DLLprependHTML(id,html) locals temp do
 temp[1]=id temp[2]=html temp[3]='PREPEND'
 CLEAR_DIVSTORE=appends(CLEAR_DIVSTORE,temp)
 return '' /return
/function





function DLLrunSCRIPT(js) do
 o=writeHTML('evalJS',js)
 return '' /return
/function





function DLLajax.clicked() locals temp,a do
 if AJAX.clicked<>"ERROR" then return AJAX.clicked /return /if
 a=getenv('QUERY_STRING')
 if a="ERROR" or a='' then a=sysstdin else if sysstdin<>'ERROR' and sysstdin<>'' then a=a+'&'+sysstdin /if /if
 if count(a,'AJAX.clicked=')>0 then
  AJAX.clicked=chopchopleftright(a,'AJAX.clicked=','&')
 /if
 if AJAX.clicked="form" then AJAX.clicked=HTMLOS.CLICKED /if
 return AJAX.clicked /return
/function

# onceHTML is used internally by clearjs when a div is written to a variable is written with its contents CLEAR_divID
  if the page reloads the div can contain onceHTML('divID') to show what the ajaxrequest would have shown
  Seems a bit inefficient as the variable could cause the session to grow very large over time

  showonce simple shows a variable then resets it
/#
function DLLonceHTML(a) locals b do if length(a)>64 then return "ERROR: Send by reference i.e. onceHTML('x')" /return /if a='CLEAR_'+a b=@a @a='ERROR' if b='ERROR' or GLOBALRESET='TRUE' then b='' /if return b /return /function
function DLLshowonce(a) locals b do if length(a)>64 then return "ERROR: Send by reference i.e. showonce('x')" /return /if b=@a @a='ERROR' if b='ERROR' or GLOBALRESET='TRUE' then b='' /if return b /return /function

function DLLreset() do
 GLOBALRESET='ERROR'
 return '' /return
/function






















$(replaceall(^


function DLLchecked_option(`n,`v) locals `x,`y,`list do
 `list=get(`n)
 `y=' name="'+`n+'" value="'+`v+'" '
 for name=`list rowname=`x do
  if `x[1]=`v then `y=`y+'CHECKED ' /if
 /for
 return `y /return
/function


function DLLselect_option(`n,`v) locals `x,`y,`list do
 `list=get(`n)
 `y=' value="'+`v+'" '
 for name=`list rowname=`x do
  if `x[1]=`v then `y=`y+'SELECTED ' /if
 /for
 return `y /return
/function

^,'`','sdkljdc8_kl_j7_snbku3986dlkh_98y'))




function CLEAR_arrayJS(x) locals i,j do
 j='['
 for name=i value=1 to rows(x) do
  j=j+'"'+replaceall(replaceall(x[1,i],'"','\"'),lf,'\n')+'",'
 /for
 j=left(j,length(j)-1)+']'
 return j /return
/function






function DLLajaxrequest() locals temp,a do
 if ajaxrequest="TRUE"   then return "TRUE" /return /if
 if ajaxrequest="IFRAME" then return "TRUE" /return /if
 a=getenv('QUERY_STRING')
 if a="ERROR" or a='' then a=sysstdin /if
 if count(a,'ajaxrequest=TRUE')>0 or ajaxrequest="TRUE" then
  ajaxrequest="TRUE"
  return 'TRUE' /return
 elif count(a,'ajaxrequest=IFRAME')>0 or ajaxrequest="IFRAME" then
  ajaxrequest="IFRAME"
  return 'TRUE' /return
 else
  return 'FALSE' /return
 /if
/function







function DLLJSONescape(text) do
 text=replaceall(text,'"','\"')
 text=replaceall(text,'\','\\')
 text=replaceall(text,'/','\/')
 text=replaceall(text,ascii(8),'\b')
 text=replaceall(text,ascii(12),'\f')
 text=replaceall(text,lf,'\n')
 text=replaceall(text,cr,'\r')
 text=replaceall(text,ascii(9),'\t')
 # 
 #  NOT Part of the spec according to www.json.org
 #  text=replaceall(text,"'","\'")
/#
 return text /return
/function


function DLLCLEAR_JSONescape(text) do
 text=replaceall(text,'\','\\')
 text=replaceall(text,'"','\"')
 text=replaceall(text,"'","\'")
 text=replaceall(text,'/','\/')
 text=replaceall(text,lf,'\n')
 text=replaceall(text,cr,'\r')
 text=replaceall(text,ascii(8),'\b')
 text=replaceall(text,ascii(9),'\t')
 text=replaceall(text,ascii(12),'\f')
 # Originally wrote to escape single quotes too. This function is retained for legacy reasons /#
 return text /return
/function










function CLEAR_PUSH_DIVSTORE() locals divs,x,temp,temp1,q,text,ar,d,tp,o do
 q='' temp=''
 if CLEAR_DIVSTORE<>'' and CLEAR_DIVSTORE<>'ERROR' then
  for name=CLEAR_DIVSTORE rowname=x do
   if x[1]='evalJS' then
    temp=temp+x[2]+lf
   else
    if x[3]="APPEND" then
     q=q+x[1]+' '
     temp=temp+^CLEAR.f.$('^+x[1]+^').innerHTML+='^+CLEAR_JSONescape(x[2])+^';^+lf
    elif x[3]="PREPEND" then
     q=q+x[1]+' '
     temp=temp+^CLEAR.f.$('^+x[1]+^').innerHTML='^+CLEAR_JSONescape(x[2])+^' + CLEAR.f.$('^+x[1]+^').innerHTML ;^+lf
    else
     q=q+x[1]+' '
     temp=temp+^CLEAR.f.$('^+x[1]+^').innerHTML='^+CLEAR_JSONescape(x[2])+^';^+lf
     tp='CLEAR_'+x[1] @tp='ERROR' tp='ERROR'
    /if
   /if
  /for
 /if
 if q<>'' and q<>'ERROR' then
  temp=temp+^CLEAR.f.report('info','DIV Updates Complete: ^+q+^');^
 /if
 d=now d=lf+
 "Expires: Mon, 26 Jul 1997 05:00:00 GMT"+lf+
 "Last-Modified: "+left(getweekday(d,'short'),3)+', '+getday(d)+' '+getmonth(d,'short')+' '+getyear(d,'long')+' 23:59:59 GMT'+lf+
 "Cache-Control: no-cache, must-revalidate"+lf+
 "Pragma: no-cache"
 CLEAR_DIVSTORE='ERROR'
 ajax.CLICKED='ERROR'
 if left(version,4)='4.54' then
  pushval=temp
  goto "/apps/clear/version4.54.html"
 else
  if ajaxrequest='IFRAME' then
   ajaxrequest='ERROR'
   temp=replaceall(temp,'&lt;','&#60;')
   temp=replaceall(temp,'&gt;','&#62;')
   temp=replaceall(temp,'&amp;','&#38;')
   temp=replaceall(temp,'<','&lt;')
   o=webpush(temp,"text/plain;charset=UTF-8"+d+lf+"Content-Disposition: inline; filename=ajax.txt;")
  else
   ajaxrequest='ERROR'
   o=webpush('//CLEAR.js'+lf+temp,"application/javascript;charset=UTF-8"+d)
  /if
 /if
/function




function DLLAJAXpush(c,t) locals d,o do
 d=now d=lf+
 "Expires: Mon, 26 Jul 1997 05:00:00 GMT"+lf+
 "Last-Modified: "+left(getweekday(d,'short'),3)+', '+getday(d)+' '+getmonth(d,'short')+' '+getyear(d,'long')+' 23:59:59 GMT'+lf+
 "Cache-Control: no-cache, must-revalidate"+lf+
 "Pragma: no-cache"
 CLEAR_DIVSTORE='ERROR'
 ajax.CLICKED='ERROR'
 ajaxrequest='ERROR'
 o=webpush(c,t+d)
/function




function DLLisjsonnumber(n) do
 if (n*0=0 and length(n*1)=length(n)) then return 'TRUE' /return else return 'FALSE' /return /if
/function
